shape main
{
	layouttype="border";
	noshadow=true;
	defsize(90,70);

	if(hasproperty("type","association"))
	{
		setpenwidth(2);
		moveto(0,0);
		lineto(100,0);
	}
	else
	{
		rectangle(0,0,100,100);
		addsubshape("padding","n");
		addsubshape("name","center");

		shape padding
		{
			preferredheight=20;
		}

		shape name
		{
			h_align="center";
			print("#name#");
		}
	}
}

decoration network
{
	orientation="ne";
	
	setfillcolor(getuserbordercolor());
	ellipse(20,0,40,20);
	ellipse(0,80,20,100);
	ellipse(80,0,100,20);
	ellipse(60,80,80,100);
	moveto(30,10);
	lineto(90,10);
	lineto(70,90);
	lineto(10,90);
	lineto(30,10);
}

decoration composite
{
	orientation="SE";
	if(hasproperty("iscomposite","true"))
	{
		ellipse(0,40,40,60);
		ellipse(60,40,100,60);
		moveto(30,50);
		lineto(70,50);
	}
}

shape source
{
	if(hasproperty("direction","bi-directional"))
	{
		setpenwidth(2);
		moveto(16,-6);
		lineto(0,0);
		lineto(16,6);
	}
	if(hasproperty("direction","destination -> source"))
	{
		setpenwidth(2);
		moveto(16,-6);
		lineto(0,0);
		lineto(16,6);
	}
}

shape target
{
	if(hasproperty("direction","bi-directional"))
	{
		setpenwidth(2);
		moveto(16,-6);
		lineto(0,0);
		lineto(16,6);
	}
	if(hasproperty("direction","source -> destination"))
	{
		setpenwidth(2);
		moveto(16,-6);
		lineto(0,0);
		lineto(16,6);
	}
}

label middlebottomlabel
{
	print("");
}
